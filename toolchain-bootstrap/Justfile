extra_args := if path_exists("secrets") == "true" { "--secret id=secrets,type=file" } else { "" }

# An LLVM+Clang toolchain using GNU libraries and runtimes.
llvm-gnu-linux-x86-64:
  just _build_and_copy_gnu_linux x86_64 linux/amd64 jessie

llvm-gnu-linux-aarch64:
  just _build_and_copy_gnu_linux aarch64 linux/arm64 stretch

_build_and_copy_gnu_linux arch platform base_image:
   #!/usr/bin/env bash
   set -euxo pipefail
   export BUILDKIT_STEP_LOG_MAX_SIZE=-1
   export BUILDKIT_STEP_LOG_MAX_SPEED=-1
   mkdir -p build
   docker buildx build {{extra_args}} --platform {{platform}} --build-arg BASE_IMAGE={{base_image}} --build-arg SOURCE_DATE_EPOCH=$(git log -1 --pretty=%ct) --iidfile build/imageid.txt .
   cid=$(docker create --rm $(cat build/imageid.txt))
   docker cp ${cid}:/build/llvm.tar.zst build/llvm-gnu_only-{{arch}}-unknown-linux-gnu.tar.zst
   docker rm -v ${cid}

# An LLVM+Clang toolchain for macOS targeting the native host.
_apple-native tool:
  #!/usr/bin/env bash
  set -euxo pipefail

  if [ ! -d build/venv-macos ]; then
    python3 -m venv build/venv-macos
    build/venv-macos/bin/pip install -r support/requirements.clang-macos.txt
  fi

  build/venv-macos/bin/python3 scripts/clang-macos.py {{ tool }} build

llvm-apple-native:
  just _apple-native llvm

_gh_download workflow commit:
  #!/usr/bin/env bash
  set -euxo pipefail

  id=$(gh run list --workflow {{ workflow }} --json conclusion,databaseId,headSha,status | jq --raw-output '.[] | select(.status == "completed" and .conclusion == "success" and .headSha == "{{ commit }}").databaseId' | head -n 1)
  gh run download --dir dist/artifact $id

_release_download_artifacts commit:
  just _gh_download clang-linux.yml {{ commit }}
  just _gh_download clang-macos.yml {{ commit }}

_release_prepare_artifacts commit version tag:
  rm -rf dist/artifact dist/upload
  just _release_download_artifacts {{ commit }}

  mkdir -p dist/upload

  mv dist/artifact/toolchain-linux-aarch64/llvm-gnu_only-aarch64-unknown-linux-gnu.tar.zst dist/upload/llvm-{{ version }}+{{ tag }}-gnu_only-aarch64-unknown-linux-gnu.tar.zst
  mv dist/artifact/toolchain-linux-x86-64/llvm-gnu_only-x86_64-unknown-linux-gnu.tar.zst dist/upload/llvm-{{ version }}+{{ tag }}-gnu_only-x86_64-unknown-linux-gnu.tar.zst
  mv dist/artifact/toolchain-macos-14/llvm-aarch64-apple-darwin.tar.zst dist/upload/llvm-{{ version }}+{{ tag }}-aarch64-apple-darwin.tar.zst
  mv dist/artifact/toolchain-macos-13/llvm-x86_64-apple-darwin.tar.zst dist/upload/llvm-{{ version }}+{{ tag }}-x86_64-apple-darwin.tar.zst

_release_upload commit tag:
  gh release create \
    --prerelease \
    --target {{commit}} \
    --title 'Toolchains {{tag}}' \
    toolchain-bootstrap/{{tag}}
  gh release upload --clobber toolchain-bootstrap/{{tag}} dist/upload/*

release commit llvm_version tag:
  just _release_prepare_artifacts {{ commit }} {{ llvm_version }} {{ tag }}
  just _release_upload {{ commit }} {{ tag }}
